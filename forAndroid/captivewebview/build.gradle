// Copyright 2022 VMware, Inc.
// SPDX-License-Identifier: BSD-2-Clause

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'

    // The dokka plugin requires a version or it won't be found.
    // Adds the documentation/dokka task.
    // id 'org.jetbrains.dokka' version '0.9.18'
    id 'org.jetbrains.dokka-android' version '0.9.18'
}

android {
    compileSdk 32

    sourceSets {
        main.assets.srcDirs += new RelativePath(
                false, "Sources", "CaptiveWebView", "Resources"
        ).getFile(buildscript.sourceFile.parentFile.parentFile.parentFile)
    }

    defaultConfig {
        minSdk 26
        targetSdk 32
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.webkit:webkit:1.5.0'
}

// Reference for Maven publication of Android library.
// https://developer.android.com/studio/publish-library/upload-library
//
// The `publish` Gradle task seems to do what the old `uploadArchives` task did.
publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.example.captivewebview'
            artifactId = 'captivewebview'
            version = '7.0'
            afterEvaluate {
                from components.release
            }
        }
    }
    repositories {
        maven {
            url = uri(new File(rootDir, '../m2repository'))
        }
    }
}

// https://www.kotlinresources.com/library/dokka/
// https://github.com/Kotlin/dokka/issues/224#issuecomment-383886215

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaAndroidTask)  {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/dokkaJavadoc"
    includes = ['src/documentation/readme.md', 'src/documentation/extra.md']
}

task dokkaHTML(type: org.jetbrains.dokka.gradle.DokkaAndroidTask) {
    outputFormat = 'html'
    outputDirectory = "$buildDir/dokkaHTML"
    includes = ['src/documentation/readme.md', 'src/documentation/extra.md']
}
